buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            //url = "https://files.minecraftforge.net/maven"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        //classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath ('com.anatawa12.forge:ForgeGradle:1.2-1.0.+') {
            changing = true
        }
        classpath 'org.ajoberstar:gradle-git:+'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

apply plugin: 'java'
compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}
apply plugin: 'forge'
apply plugin: 'maven-publish'

import org.ajoberstar.grgit.Grgit

def repo = Grgit.open(project.file('.'))
project.ext.gitHash = repo.log().find().abbreviatedId
def branchname = repo.branch.getCurrent().fullName
if (branchname.contains('HEAD')) {
    branchname = System.getenv("GIT_BRANCH")
}
branchname = branchname.substring(branchname.lastIndexOf('/') + 1)

boolean dev = System.getenv("RELEASE") == null || System.getenv("RELEASE").equalsIgnoreCase("false")

ext.buildnumber = 0
project.buildnumber = System.getenv('BUILD_NUMBER') == null ? "CUSTOM" : System.getenv('BUILD_NUMBER')

version = dev ? "${branchname}-${buildnumber}-${project.ext.gitHash}" : "${mod_version}.${buildnumber}"
group = "team.chisel" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Chisel"

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"

    replaceIn "Chisel.java"
    replace "@VERSION@", project.version
}

configurations {
    shade
    compile.extendsFrom shade
    compile.extendsFrom exportedCompile
}

repositories {
	//maven {
	//	name 'Chicken Bones Repo'
	//	url "https://chickenbones.net/maven/"
	//}

    //maven {
	//	name 'Prof Mobius Repo'
	//	url "https://mobiusstrip.eu/maven"
	//}
    
    maven {
        name 'Chisel Repo'
        url "https://maven.tterrag.com/"
    }
}

dependencies {
    compile files('libs/CTMLib-MC1.7.10-1.4.1.5.jar')
    compile files('libs/EquivalentExchange3-1.7.10-0.3.0.547.jar')
    compile files('libs/Waila-1.5.10_1.7.10.jar')
    compile files('libs/CodeChickenLib-1.7.10-1.1.3.141-universal.jar')
    compile files('libs/CodeChickenCore-1.7.10-1.0.7.48-universal.jar')
    compile files('libs/NotEnoughItems-1.7.10-2.1.18-GTNH.jar')
    compile files('libs/ForgeMultipart-1.7.10-1.2.0.345-universal.jar')
    
    shade ("team.chisel.ctmlib:CTMLib:MC1.7.10-1.4.1.5") {
        transitive = false
    }
}

processResources
        {
            // this will ensure that this task is redone when the versions change.
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            // replace stuff in mcmod.info, nothing else
            from(sourceSets.main.resources.srcDirs) {
                include 'src/main/resources/mcmod.info'

                // replace version and mcversion
                expand 'version': project.version, 'mcversion': project.minecraft.version
            }

            // copy everything else, thats not the mcmod.info
            // copy with absolute dir (src/main/resources dirs)
            from(sourceSets.main.resources.srcDirs) {
                exclude 'assets/*'
                exclude 'META-INF/*'
                exclude 'banner.jpg'
                exclude 'LICENSE'
                exclude 'mcmod.info'
                exclude 'pack.mcmeta'
                exclude 'src/main/resources/mcmod.info'
            }
        }
        
jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**', '*.info'
        }
    }
    
    manifest {
        attributes 'FMLAT': 'chisel_compile_at.cfg'
    }
}

// Create deobf dev jars
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

// Create API jar with sources and compiled files
task apiJar(type: Jar) {
    from sourceSets.main.allSource
    from sourceSets.main.output
	include 'com/cricketcraft/chisel/api/**/*'
    classifier = 'api'
}

// Create source attachment for dev jar
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

artifacts {
    archives deobfJar
    archives sourcesJar
    archives apiJar
}

//publishing {
//    tasks.publish.dependsOn 'build'
//    publications {
//        mavenJava(MavenPublication) {
//            artifact jar
//            artifact deobfJar
//            artifact sourcesJar
//            artifact apiJar
//        }
//    }
//
//    repositories {
//        if (project.hasProperty('mavendir')) {
//            maven { url mavendir }
//        }
//    }
//}

println('Changelog:\n' + getChangelogText())

String getChangelogText() {
    def changelogFile = file('changelog.txt')
    String str = ''
    int lineCount = 0
    boolean done = false
    changelogFile.eachLine {
        if (done || it == null) {
            return
        }
        if (it.size() > 1) {
            def temp = it
            if (lineCount == 0) {
                temp = "Chisel ${version}"
                temp = "<h2>$temp</h2>"
            } else if (it.startsWith('-')) {
                temp = "&nbsp;&nbsp;&nbsp;$temp"
            } else {
                temp = "<h4>$temp</h4>"
            }
            str += "$temp<br/>"
            lineCount++
            return
        } else {
            done = true
        }
    }
    return str
}

tasks.curseforge.dependsOn 'reobf'
tasks.curseforge.enabled = !dev && project.hasProperty('curseforge_key')
    
curseforge {
    if (project.hasProperty('curseforge_key')) {
        apiKey = project.curseforge_key
    }

    project {
        id = project.projectId
        changelog = System.getenv('CHANGELOG') == null || System.getenv('CHANGELOG').equals('none') ? getChangelogText() : System.getenv('CHANGELOG')
        changelogType = 'html'
        releaseType = 'release'
        relations {
            embeddedLibrary 'ctmlib'
        }
        mainArtifact(jar) {
            displayName = "Chisel - ${version}"
        }
        addArtifact deobfJar
        addArtifact sourcesJar
        addArtifact(apiJar) {
            relations {}
        }
    }
}

idea { module { inheritOutputDirs = true } }
